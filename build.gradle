plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'maven-publish'

group 'dev.bothin.testcontainers'
version '0.0.2'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0-RC2'

    implementation "org.testcontainers:testcontainers:1.11.3"
    implementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'

    testImplementation "org.testcontainers:junit-jupiter:1.11.3"
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact project.sourcesJar {
                classifier "sources"
            }
            groupId 'dev.bothin.testcontainers'
            artifactId 'junit-jupiter-suite-test-containers-lifecycle'
            version '0.0.2'
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'junit-jupiter-suite-test-containers-lifecycle'
        name = 'junit-jupiter-suite-test-containers-lifecycle'
        userOrg = user
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hemma/junit-jupiter-suite-test-containers-lifecycle.git'
        labels = ['test-containers', 'junit-jupiter', 'kotlin', 'java']
        publicDownloadNumbers = true
        version {
            name = '0.0.2'
            desc = ''
            vcsTag = '0.0.2'
        }
    }
}